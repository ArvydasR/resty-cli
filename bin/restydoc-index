#!/usr/bin/env perl

use strict;
use warnings;

use FindBin ();
use File::Find ();
use Cwd qw( cwd );
use File::Spec ();

my $indir = shift
    or die "no input project directory name specified.\n";

if (!-d $indir) {
    die "input directory $indir not found.\n";
}

$indir =~ s{/+$}{}g;

(my $full_dist_name = $indir) =~ s{.*/}{};
(my $dist_name = $full_dist_name) =~ s{ [-_] v? ( \d+ (?:\.\d+)* (?: rc\d+ | _\d+ )? ) $ }{}i;
my $dist_ver = $1;

#print "name: $full_dist_name\n";
#if (defined $dist_ver) {
    #print "version: $dist_ver\n";
#}

my @aliases = gen_aliases($indir, $dist_name);
#print "aliases: @aliases\n";

my $poddir = "$FindBin::Bin/../pod";

if (!-d $poddir) {
    mkdir $poddir
        or die "failed to create $poddir: $!\n";
}

my @dist_modules;

File::Find::find(\&wanted,  $indir);

my $index = "dist $dist_name\n";

if ($dist_ver) {
    $index .= "  version $dist_ver\n";
}

if (@aliases) {
    $index .= "  aliases @aliases\n";
}

if (@dist_modules) {
    $index .= "  modules @dist_modules\n";
}

$index .= "\n";

my $index_file = "resty.index";
open my $out, ">>$index_file"
    or die "cannot open $index_file for appending: $!\n";
print $out $index;
close $out;

sub wanted {
    return unless -f $_ && m/ \. (?: md | markdown) $ /x;
    my $mdfile = $File::Find::name;

    my $name = File::Spec->abs2rel($mdfile, $indir);
    return if $name =~ /^node_modules/;

    $name =~ s{ ^ (?: lib | src | lua ) / }{}x;
    $name =~ s{ \. \w+ $ }{}x;
    $name =~ s{/}{.}g;

    return if length($name) == 1;

    #warn $name;
    #warn "wanted: $File::Find::dir $File::Find::name $_\n";
    if ($name =~ / ^ README $ /xi) {
        $name = $dist_name;
    }

    my $podfile = "$poddir/$name.pod";
    shell("$FindBin::Bin/md2pod.pl -o $podfile $mdfile");

    process_pod($podfile);

    push @dist_modules, $name;
}

sub process_pod {
    my $infile = shift;

    open my $in, "<:encoding(UTF-8)", $infile or
        die "cannot open $infile for reading: $!\n";

    my ($level, $new);
    while (<$in>) {
        if (defined $level) {
            if (/ ^ =head (\d+) /x && $1 >= $level) {
                undef $level;
                next;
            }

            # ignore the content
            next;
        }

        # !defined $level

        if (/ ^ =head (\d+) \s+ Table \s+ of \s+ Contents? \s* $ /ix) {
            $level = $1;
            # ignore the content
            next;
        }

        $new .= $_;
    }

    close $in;

    open my $out, ">:encoding(UTF-8)", $infile
        or die "cannot open $infile for writing: $!\n";
    print $out $new;
    close $out;
}

sub shell {
    my $cmd = shift;

    #warn $cmd;
    system($cmd) == 0
        or die "failed to run command \"$cmd\": $!\n";
}

sub gen_aliases {
    my ($indir, $name) = @_;

    my @aliases;
    if ($name =~ s/-nginx-module$//) {
        $name =~ s/-/_/g;
        $name = "ngx_" . $name;
        push @aliases, $name;
    }

    if ($name =~ / ^ lua (?: - \w+ )+ $ /x) {
        $name =~ s/^lua-//;
        $name =~ s/-/./g;
        push @aliases, $name;
    }

    my $config_file = "$indir/config";
    if (-f $config_file) {
        open my $in, $config_file
            or die "cannot open $config_file for reading: $!\n";
        while (<$in>) {
            if (/ \b ngx_addon_name = .*? (\w+) /x) {
                my $addon = $1;
                push @aliases, $addon;
                last;
            }
        }
        close $in;
    }

    return @aliases;
}
